#!/usr/local/bin/perl -w

# This script displays all the routers interfaces and their throughput
#
#
# $Id: //ariba/services/monitor/bin/network/switch-usage#9 $

use strict;
use File::Basename;
use FindBin;
use lib "$FindBin::Bin/../../lib";

use ariba::rc::InstalledProduct;
use ariba::Ops::Machine;
use ariba::Ops::ProductAPIExtensions;
use ariba::Ops::NetworkUtils;
use ariba::monitor::CircularDB;
use ariba::monitor::QueryManager;
use ariba::monitor::StatusPage;
use ariba::monitor::Query;
use dmail::LockLib;

my $lockFile = "/tmp/" . basename($0);

sub main {
	my $sendEmail = 0;
	my $sendPage = 0;
	my $debug = 0;

	while(my $arg=shift(@ARGV)){
		if($arg =~ /^-d/o){ $debug = 1; };
		if($arg =~ /^-e/o){ $sendEmail = 1; };
		if($arg =~ /^-p/o){ $sendPage = 1; };
	}

	die "can't grab lock\n" unless dmail::LockLib::requestlock($lockFile, 5);

	my $me = ariba::rc::InstalledProduct->new();
	my @dataCenters = ariba::Ops::ProductAPIExtensions::datacentersForProducts($me);

	my $cluster = $me->currentCluster();

	my $warn;
	my $crit;
	my $maxFastEth = 100;
	my $maxGigaEth = 1000;

	my %queries = ();

	for my $datacenter ( @dataCenters ) {
		for my $deviceType ('router', 'switch', 'loadbalancer', 'firewall') {

			my %matchDatacenter = (
					status     => 'inservice',
					datacenter => $datacenter,
					monitoringDatacenter => '',
					providesServices => $deviceType,
					);

			my %matchMonitoringDatacenter = (
					status     => 'inservice',
					monitoringDatacenter => $datacenter,
					providesServices => $deviceType,
					);

			my @devices = ariba::Ops::Machine->machinesWithProperties(%matchDatacenter);
			@devices = (@devices , ariba::Ops::Machine->machinesWithProperties(%matchMonitoringDatacenter));

			for my $device (@devices) {
				my $deviceName = $device->hostname();
				
				my $uiHint = "$deviceType";

				# average over 5 samples by default
				my $samples = 5;

				$warn = 0.8 * $maxFastEth;
				$crit = 0.9 * $maxFastEth;

				# We cannot retrieve everything in one call or
				# it would run out of files handles
				my @toDisplay = qw(OutOctets InOctets);

				if ($debug) {
					print "working on $deviceType [$deviceName] in $datacenter\n";
				}


				for my $item (@toDisplay) {
					my $cdbs     = ariba::monitor::CircularDB->listDBs("snmp/$deviceName", $item);

					for my $cdb (@$cdbs) {

						my $cdbTrueName = $cdb->name(1);
						$cdbTrueName =~ s/ /_/g;

						# IOS reports IN/OUT statistics for VLAN. We only care about
						# Physical interfaces. See TMID: 61199 and 60545
						next unless ( $cdbTrueName !~ /VLAN/i);

						# Ignoring internal data interfaces for firewalls See TMID: 115334
						next if ($deviceType eq 'firewall' && $cdbTrueName =~ /Internal-Data/i);

						my $query = ariba::monitor::Query->new("snmp/" . $cdbTrueName);

						my $maxSpeed = 100;
						if ($query->portSpeed()){
							$maxSpeed = $query->portSpeed();
						}

						print "The maximum throughput is $maxSpeed Mb/s for interface " . $cdb->name() if ($debug);

						$warn = 0.8 * $maxSpeed;
						$crit = 0.9 * $maxSpeed;
						my $critString = "answer ne '' and answer > $crit";
						
						# If it's a Gigabit interface
						# The limit has to be updated
						#
						# Examples :
						# c3640-n1-1.opslabdr.ariba.com/FastEthernet1:0 ifInOctets MBits
						# c3750-n3-2.opslab.ariba.com/GigabitEthernet1:0:1 ifOutOctets MBits
						#
						my $intf = $cdb->name();
						if( $intf =~ m|backup10|)
						{
							$critString = undef;
						}
						
						$intf =~ s/$deviceName\/(\S+).*/$1/;
						print "Intf : [$intf]\n\n" if ($debug);

						my $file = $cdb->fileName();

						my $cdbName = "average($file, $samples)";


						my $format = "%.1f of " . $maxSpeed . " Mbit/s\n";
                        my $metricDescription = $cdb->name();

						$queries{$cdb->name()} = {
							uiHint => $uiHint,
							format => $format,
							info => "answer eq '' or answer > -1",
							warn => "answer eq '' or answer > $warn",
							crit => $critString,
                                                        hasErraticSchedule => 1,
							extractRecorded => $cdbName,
                            inf_field => "usage",
                            inf_tags => qq|deviceType="$deviceType",deviceName="$deviceName",metricDescription="$metricDescription"|,
						};

					}
				}
			}

		}
		next unless scalar(keys(%queries));

		my $email = $me->default('notify.email');

		my $pn = ariba::monitor::StatusPage::fakeProductNameForDatacenter($datacenter);

		my $q = ariba::monitor::QueryManager->newWithDetails("network-usage", $pn, $me->service(), $me->customer(), \%queries);

		$q->processQueriesUsingServer($debug, $email, $sendEmail, $sendPage);

		# After each datacenter reinit queries hash.
		%queries  = ();
	}
	dmail::LockLib::releaselock($lockFile);
}

main();

__END__
